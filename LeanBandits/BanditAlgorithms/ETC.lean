/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import LeanBandits.AlgorithmAndRandomVariables
import LeanBandits.ForMathlib.MeasurableArgMax
import LeanBandits.ForMathlib.SubGaussian
import LeanBandits.RewardByCountMeasure

/-! # The Explore-Then-Commit Algorithm

-/

open MeasureTheory ProbabilityTheory Finset Learning
open scoped ENNReal NNReal

lemma ae_eq_set_iff {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±} {s t : Set Î±} :
    s =áµ[Î¼] t â†” âˆ€áµ a âˆ‚Î¼, a âˆˆ s â†” a âˆˆ t := by
  rw [Filter.EventuallyEq]
  simp only [eq_iff_iff]
  congr!

--todo: generalize Icc
lemma measurable_sum_of_le {Î± : Type*} {mÎ± : MeasurableSpace Î±}
    {f : â„• â†’ Î± â†’ â„} {g : Î± â†’ â„•} {n : â„•} (hg_le : âˆ€ a, g a â‰¤ n) (hf : âˆ€ i, Measurable (f i))
    (hg : Measurable g) :
    Measurable (fun a â†¦ âˆ‘ i âˆˆ Icc 1 (g a), f i a) := by
  have h_eq : (fun a â†¦ âˆ‘ i âˆˆ Icc 1 (g a), f i a)
      = fun a â†¦ âˆ‘ i âˆˆ range (n + 1), if g a = i then âˆ‘ j âˆˆ Icc 1 i, f j a else 0 := by
    ext Ï‰
    rw [sum_ite_eq_of_mem]
    grind
  rw [h_eq]
  refine measurable_sum _ fun n hn â†¦ ?_
  refine Measurable.ite ?_ (by fun_prop) (by fun_prop)
  exact (measurableSet_singleton _).preimage (by fun_prop)

namespace Bandits

variable {K : â„•}

/-- Arm pulled by the ETC algorithm at time `n + 1`. -/
noncomputable
def ETC.nextArm (hK : 0 < K) (m n : â„•) (h : Iic n â†’ Fin K Ã— â„) : Fin K :=
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  if hn : n < K * m - 1 then
    âŸ¨(n + 1) % K, Nat.mod_lt _ hKâŸ© -- for `n = 0` we have pulled arm 0 already, and we pull arm 1
  else
    if hn_eq : n = K * m - 1 then measurableArgmax (empMean' n) h
    else (h âŸ¨n, by simpâŸ©).1

@[fun_prop]
lemma ETC.measurable_nextArm (hK : 0 < K) (m n : â„•) : Measurable (nextArm hK m n) := by
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  unfold nextArm
  simp only [dite_eq_ite]
  refine Measurable.ite (by simp) (by fun_prop) ?_
  refine Measurable.ite (by simp) ?_ (by fun_prop)
  exact measurable_measurableArgmax fun a â†¦ by fun_prop

/-- The Explore-Then-Commit algorithm. -/
noncomputable
def etcAlgorithm (hK : 0 < K) (m : â„•) : Algorithm (Fin K) â„ :=
  detAlgorithm (ETC.nextArm hK m) (by fun_prop) âŸ¨0, hKâŸ©

namespace ETC

variable {hK : 0 < K} {m : â„•} {Î½ : Kernel (Fin K) â„} [IsMarkovKernel Î½]

local notation "ğ”“t" => Bandit.trajMeasure (etcAlgorithm hK m) Î½
local notation "ğ”“" => Bandit.measure (etcAlgorithm hK m) Î½

lemma arm_zero : arm 0 =áµ[ğ”“t] fun _ â†¦ âŸ¨0, hKâŸ© := by
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  exact arm_zero_detAlgorithm

lemma arm_ae_eq_etcNextArm (n : â„•) :
    arm (n + 1) =áµ[ğ”“t] fun h â†¦ nextArm hK m n (fun i â†¦ h i) := by
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  exact arm_detAlgorithm_ae_eq n

lemma arm_of_lt {n : â„•} (hn : n < K * m) :
    arm n =áµ[ğ”“t] fun _ â†¦ âŸ¨n % K, Nat.mod_lt _ hKâŸ© := by
  cases n with
  | zero => exact arm_zero
  | succ n =>
    filter_upwards [arm_ae_eq_etcNextArm n] with h hn_eq
    rw [hn_eq, nextArm, dif_pos]
    grind

lemma arm_mul (hm : m â‰  0) :
    have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
    arm (K * m) =áµ[ğ”“t] fun h â†¦ measurableArgmax (empMean' (K * m - 1)) (fun i â†¦ h i) := by
  have : K * m = (K * m - 1) + 1 := by
    have : 0 < K * m := Nat.mul_pos hK hm.bot_lt
    grind
  rw [this]
  filter_upwards [arm_ae_eq_etcNextArm (K * m - 1)] with h hn_eq
  rw [hn_eq, nextArm, dif_neg (by simp), dif_pos rfl]
  exact this â–¸ rfl

lemma arm_add_one_of_ge {n : â„•} (hm : m â‰  0) (hn : K * m â‰¤ n) :
    arm (n + 1) =áµ[ğ”“t] fun Ï‰ â†¦ arm n Ï‰ := by
  filter_upwards [arm_ae_eq_etcNextArm n] with Ï‰ hn_eq
  rw [hn_eq, nextArm, dif_neg (by grind), dif_neg]
  Â· rfl
  Â· have : 0 < K * m := Nat.mul_pos hK hm.bot_lt
    grind

lemma arm_of_ge {n : â„•} (hm : m â‰  0) (hn : K * m â‰¤ n) : arm n =áµ[ğ”“t] arm (K * m) := by
  have h_ae n : K * m â‰¤ n â†’ arm (n + 1) =áµ[ğ”“t] fun Ï‰ â†¦ arm n Ï‰ := arm_add_one_of_ge hm
  simp_rw [Filter.EventuallyEq, â† ae_all_iff] at h_ae
  filter_upwards [h_ae] with Ï‰ h_ae
  induction n, hn using Nat.le_induction with
  | base => rfl
  | succ n hmn h_ind => rw [h_ae n hmn, h_ind]

lemma sum_mod_range {K : â„•} (hK : 0 < K) (a : Fin K) :
    (âˆ‘ s âˆˆ range K, if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) = 1 := by
  have h_iff (s : â„•) (hs : s < K) : âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a â†” s = a := by
    simp only [Nat.mod_eq_of_lt hs, Fin.ext_iff]
  calc (âˆ‘ s âˆˆ range K, if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0)
  _ = âˆ‘ s âˆˆ range K, if s = a then 1 else 0 := sum_congr rfl fun s hs â†¦ by grind
  _ = _ := by
    rw [sum_ite_eq']
    simp

lemma sum_mod_range_mul {K : â„•} (hK : 0 < K) (m : â„•) (a : Fin K) :
    (âˆ‘ s âˆˆ range (K * m), if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) = m := by
  induction m with
  | zero => simp
  | succ n hn =>
    calc (âˆ‘ s âˆˆ range (K * (n + 1)), if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0)
    _ = (âˆ‘ s âˆˆ range (K * n + K), if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) := by ring_nf
    _ = (âˆ‘ s âˆˆ range (K * n), if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0)
        + (âˆ‘ s âˆˆ Ico (K * n) (K * n + K), if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) := by
      rw [sum_range_add_sum_Ico]
      grind
    _ = n + (âˆ‘ s âˆˆ Ico (K * n) (K * n + K), if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) := by
      rw [hn]
    _ = n + (âˆ‘ s âˆˆ range K, if âŸ¨(s + K * n) % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) := by
      congr 1
      let e : â„• â†ª â„• := âŸ¨fun i : â„• â†¦ i + K * n, fun i j hij â†¦ by grindâŸ©
      have : Finset.map e (range K) = Ico (K * n) (K * n + K) := by
        ext x
        simp only [mem_map, mem_range, Function.Embedding.coeFn_mk, mem_Ico, e]
        refine âŸ¨fun h â†¦ by grind, fun h â†¦ ?_âŸ©
        use x - K * n
        grind
      rw [â† this, Finset.sum_map]
      congr
    _ = n + (âˆ‘ s âˆˆ range K, if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) := by simp
    _ = n + 1 := by rw [sum_mod_range hK]

lemma pullCount_mul (a : Fin K) : pullCount a (K * m) =áµ[ğ”“t] fun _ â†¦ m := by
  rw [Filter.EventuallyEq]
  simp_rw [pullCount_eq_sum]
  have h_arm (n : range (K * m)) : arm n =áµ[ğ”“t] fun _ â†¦ âŸ¨n % K, Nat.mod_lt _ hKâŸ© :=
    arm_of_lt (mem_range.mp n.2)
  simp_rw [Filter.EventuallyEq, â† ae_all_iff] at h_arm
  filter_upwards [h_arm] with Ï‰ h_arm
  have h_arm' {i : â„•} (hi : i âˆˆ range (K * m)) : arm i Ï‰ = âŸ¨i % K, Nat.mod_lt _ hKâŸ© := h_arm âŸ¨i, hiâŸ©
  calc (âˆ‘ s âˆˆ range (K * m), if arm s Ï‰ = a then 1 else 0)
  _ = (âˆ‘ s âˆˆ range (K * m), if âŸ¨s % K, Nat.mod_lt _ hKâŸ© = a then 1 else 0) :=
    sum_congr rfl fun s hs â†¦ by rw [h_arm' hs]
  _ = m := sum_mod_range_mul hK m a

lemma pullCount_add_one_of_ge (a : Fin K) (hm : m â‰  0) {n : â„•} (hn : K * m â‰¤ n) :
    pullCount a (n + 1)
      =áµ[ğ”“t] fun Ï‰ â†¦ pullCount a n Ï‰ + {Ï‰' | arm (K * m) Ï‰' = a}.indicator (fun _ â†¦ 1) Ï‰ := by
  simp_rw [Filter.EventuallyEq, pullCount_add_one]
  filter_upwards [arm_of_ge hm hn] with Ï‰ h_arm
  congr

lemma pullCount_of_ge (a : Fin K) (hm : m â‰  0) {n : â„•} (hn : K * m â‰¤ n) :
    pullCount a n
      =áµ[ğ”“t] fun Ï‰ â†¦ m + (n - K * m) * {Ï‰' | arm (K * m) Ï‰' = a}.indicator (fun _ â†¦ 1) Ï‰ := by
  have h_ae n : K * m â‰¤ n â†’ pullCount a (n + 1)
      =áµ[ğ”“t] fun Ï‰ â†¦ pullCount a n Ï‰ + {Ï‰' | arm (K * m) Ï‰' = a}.indicator (fun _ â†¦ 1) Ï‰ :=
    pullCount_add_one_of_ge a hm
  simp_rw [Filter.EventuallyEq, â† ae_all_iff] at h_ae
  have h_ae_Km : pullCount a (K * m) =áµ[ğ”“t] fun _ â†¦ m := pullCount_mul a
  filter_upwards [h_ae_Km, h_ae] with Ï‰ h_Km h_ae
  induction n, hn using Nat.le_induction with
  | base => simp [h_Km]
  | succ n hmn h_ind =>
    rw [h_ae n hmn, h_ind, add_assoc, â† add_one_mul]
    congr
    grind

lemma sumRewards_bestArm_le_of_arm_mul_eq (a : Fin K) (hm : m â‰  0) :
    have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
    âˆ€áµ h âˆ‚ğ”“t, arm (K * m) h = a â†’ sumRewards (bestArm Î½) (K * m) h â‰¤ sumRewards a (K * m) h := by
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  filter_upwards [arm_mul hm, pullCount_mul a, pullCount_mul (bestArm Î½)] with h h_arm ha h_best
    h_eq
  have h_max := isMaxOn_measurableArgmax (empMean' (K * m - 1)) (fun i â†¦ h i) (bestArm Î½)
  rw [â† h_arm, h_eq] at h_max
  rw [sumRewards_eq_pullCount_mul_empMean, sumRewards_eq_pullCount_mul_empMean, ha, h_best]
  Â· gcongr
    have : 0 < K * m := Nat.mul_pos hK hm.bot_lt
    rwa [empMean_eq_empMean' this.ne', empMean_eq_empMean' this.ne']
  Â· simp [ha, hm]
  Â· simp [h_best, hm]

lemma identDistrib_aux (m : â„•) (a b : Fin K) :
    IdentDistrib
      (fun Ï‰ â†¦ (âˆ‘ s âˆˆ Icc 1 m, rewardByCount a s Ï‰.1 Ï‰.2, âˆ‘ s âˆˆ Icc 1 m, rewardByCount b s Ï‰.1 Ï‰.2))
      (fun Ï‰ â†¦ (âˆ‘ s âˆˆ range m, Ï‰.2 s a, âˆ‘ s âˆˆ range m, Ï‰.2 s b)) ğ”“ ğ”“ := by
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  have h1 (a : Fin K) :
      IdentDistrib (fun Ï‰ s â†¦ rewardByCount a (s + 1) Ï‰.1 Ï‰.2) (fun Ï‰ s â†¦ Ï‰.2 s a) ğ”“ ğ”“ :=
    identDistrib_rewardByCount_stream a
  have h2 (a : Fin K) : IdentDistrib (fun Ï‰ â†¦ âˆ‘ s âˆˆ Icc 1 m, rewardByCount a s Ï‰.1 Ï‰.2)
      (fun Ï‰ â†¦ âˆ‘ s âˆˆ range m, Ï‰.2 s a) ğ”“ ğ”“ := by
    have h_eq (Ï‰ : (â„• â†’ Fin K Ã— â„) Ã— (â„• â†’ Fin K â†’ â„)) : âˆ‘ s âˆˆ Icc 1 m, rewardByCount a s Ï‰.1 Ï‰.2
        = âˆ‘ s âˆˆ range m, rewardByCount a (s + 1) Ï‰.1 Ï‰.2 := by
      let e : Icc 1 m â‰ƒ range m :=
      { toFun x := âŸ¨x - 1, by have h := x.2; simp only [mem_Icc] at h; simp; grindâŸ©
        invFun x := âŸ¨x + 1, by
          have h := x.2
          simp only [mem_Icc, le_add_iff_nonneg_left, zero_le, true_and, ge_iff_le]
          simp only [mem_range] at h
          grindâŸ©
        left_inv x := by have h := x.2; simp only [mem_Icc] at h; grind
        right_inv x := by have h := x.2; grind }
      rw [â† sum_coe_sort (Icc 1 m), â† sum_coe_sort (range m), sum_equiv e]
      Â· simp
      Â· simp only [univ_eq_attach, mem_attach, forall_const, Subtype.forall, mem_Icc,
          forall_and_index]
        grind
    simp_rw [h_eq]
    exact IdentDistrib.comp (h1 a) (u := fun p â†¦ âˆ‘ s âˆˆ range m, p s) (by fun_prop)
  by_cases hab : a = b
  Â· simp only [hab]
    exact (h2 b).comp (u := fun p â†¦ (p, p)) (by fun_prop)
  refine (h2 a).prod (h2 b) ?_ ?_
  Â· suffices IndepFun (fun Ï‰ s â†¦ rewardByCount a s Ï‰.1 Ï‰.2) (fun Ï‰ s â†¦ rewardByCount b s Ï‰.1 Ï‰.2)
        ğ”“ by
      exact this.comp (Ï† := fun p â†¦ âˆ‘ i âˆˆ Icc 1 m, p i) (Ïˆ := fun p â†¦ âˆ‘ j âˆˆ Icc 1 m, p j)
        (by fun_prop) (by fun_prop)
    exact indepFun_rewardByCount_of_ne hab
  Â· suffices IndepFun (fun Ï‰ s â†¦ Ï‰.2 s a) (fun Ï‰ s â†¦ Ï‰.2 s b) ğ”“ by
      exact this.comp (Ï† := fun p â†¦ âˆ‘ i âˆˆ range m, p i) (Ïˆ := fun p â†¦ âˆ‘ j âˆˆ range m, p j)
        (by fun_prop) (by fun_prop)
    exact indepFun_eval_snd_measure _ Î½ hab

lemma prob_arm_mul_eq_le (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) 1 (Î½ a)) (a : Fin K)
    (hm : m â‰  0) :
    (ğ”“t).real {Ï‰ | arm (K * m) Ï‰ = a} â‰¤ Real.exp (- (m : â„) * gap Î½ a ^ 2 / 4) := by
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  have h_pos : 0 < K * m := Nat.mul_pos hK hm.bot_lt
  have h_le : (ğ”“t).real {Ï‰ | arm (K * m) Ï‰ = a}
      â‰¤ (ğ”“t).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰ â‰¤ sumRewards a (K * m) Ï‰} := by
    simp_rw [measureReal_def]
    gcongr 1
    Â· simp
    refine measure_mono_ae ?_
    exact sumRewards_bestArm_le_of_arm_mul_eq a hm
  refine h_le.trans ?_
  -- extend the probability space to include the stream of independent rewards
  suffices (ğ”“).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰.1 â‰¤ sumRewards a (K * m) Ï‰.1}
      â‰¤ Real.exp (- (m : â„) * gap Î½ a ^ 2 / 4) by
    suffices (ğ”“t).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰ â‰¤ sumRewards a (K * m) Ï‰}
      = (ğ”“).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰.1 â‰¤ sumRewards a (K * m) Ï‰.1} by rwa [this]
    calc (ğ”“t).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰ â‰¤ sumRewards a (K * m) Ï‰}
    _ = ((ğ”“).fst).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰ â‰¤ sumRewards a (K * m) Ï‰} := by simp
    _ = (ğ”“).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰.1 â‰¤ sumRewards a (K * m) Ï‰.1} := by
      rw [Measure.fst, map_measureReal_apply (by fun_prop)]
      Â· rfl
      Â· exact measurableSet_le (by fun_prop) (by fun_prop)
  calc (ğ”“).real {Ï‰ | sumRewards (bestArm Î½) (K * m) Ï‰.1 â‰¤ sumRewards a (K * m) Ï‰.1}
  _ = (ğ”“).real {Ï‰ | âˆ‘ s âˆˆ Icc 1 (pullCount (bestArm Î½) (K * m) Ï‰.1),
        rewardByCount (bestArm Î½) s Ï‰.1 Ï‰.2
      â‰¤ âˆ‘ s âˆˆ Icc 1 (pullCount a (K * m) Ï‰.1), rewardByCount a s Ï‰.1 Ï‰.2} := by
    congr with Ï‰
    congr! 1 <;> rw [sum_rewardByCount_eq_sumRewards]
  _ = (ğ”“).real {Ï‰ | âˆ‘ s âˆˆ Icc 1 m, rewardByCount (bestArm Î½) s Ï‰.1 Ï‰.2
      â‰¤ âˆ‘ s âˆˆ Icc 1 m, rewardByCount a s Ï‰.1 Ï‰.2} := by
    simp_rw [measureReal_def]
    congr 1
    refine measure_congr ?_
    have ha := pullCount_mul a (hK := hK) (Î½ := Î½) (m := m)
    have h_best := pullCount_mul (bestArm Î½) (hK := hK) (Î½ := Î½) (m := m)
    rw [ae_eq_set_iff]
    change âˆ€áµ Ï‰ âˆ‚((ğ”“t).prod _), _
    rw [Measure.ae_prod_iff_ae_ae]
    Â· filter_upwards [ha, h_best] with Ï‰ ha h_best
      refine ae_of_all _ fun Ï‰' â†¦ ?_
      rw [ha, h_best]
    Â· simp only [Set.mem_setOf_eq]
      let fâ‚ := fun Ï‰ : (â„• â†’ Fin K Ã— â„) Ã— (â„• â†’ Fin K â†’ â„) â†¦
        âˆ‘ s âˆˆ Icc 1 (pullCount (bestArm Î½) (K * m) Ï‰.1), rewardByCount (bestArm Î½) s Ï‰.1 Ï‰.2
      let gâ‚ := fun Ï‰ : (â„• â†’ Fin K Ã— â„) Ã— (â„• â†’ Fin K â†’ â„) â†¦
        âˆ‘ s âˆˆ Icc 1 (pullCount a (K * m) Ï‰.1), rewardByCount a s Ï‰.1 Ï‰.2
      let fâ‚‚ := fun Ï‰ : (â„• â†’ Fin K Ã— â„) Ã— (â„• â†’ Fin K â†’ â„) â†¦
        âˆ‘ s âˆˆ Icc 1 m, rewardByCount (bestArm Î½) s Ï‰.1 Ï‰.2
      let gâ‚‚ := fun Ï‰ : (â„• â†’ Fin K Ã— â„) Ã— (â„• â†’ Fin K â†’ â„) â†¦ âˆ‘ s âˆˆ Icc 1 m, rewardByCount a s Ï‰.1 Ï‰.2
      change MeasurableSet {x | fâ‚ x â‰¤ gâ‚ x â†” fâ‚‚ x â‰¤ gâ‚‚ x}
      have hfâ‚ : Measurable fâ‚ := by
        refine measurable_sum_of_le (n := K * m + 1)
          (g := fun Ï‰ : (â„• â†’ Fin K Ã— â„) Ã— (â„• â†’ Fin K â†’ â„) â†¦ pullCount (bestArm Î½) (K * m) Ï‰.1)
          (f := fun s Ï‰ â†¦ rewardByCount (bestArm Î½) s Ï‰.1 Ï‰.2) (fun Ï‰ â†¦ ?_)
          (by fun_prop) (by fun_prop)
        have h_le := pullCount_le (bestArm Î½) (K * m) Ï‰.1
        grind
      have hgâ‚ : Measurable gâ‚ := by
        refine measurable_sum_of_le (n := K * m + 1)
          (g := fun Ï‰ : (â„• â†’ Fin K Ã— â„) Ã— (â„• â†’ Fin K â†’ â„) â†¦ pullCount a (K * m) Ï‰.1)
          (f := fun s Ï‰ â†¦ rewardByCount a s Ï‰.1 Ï‰.2) (fun Ï‰ â†¦ ?_) (by fun_prop) (by fun_prop)
        have h_le := pullCount_le a (K * m) Ï‰.1
        grind
      refine MeasurableSet.iff ?_ ?_
      Â· exact measurableSet_le (by fun_prop) (by fun_prop)
      Â· exact measurableSet_le (by fun_prop) (by fun_prop)
  _ = (ğ”“).real {Ï‰ | âˆ‘ s âˆˆ range m, Ï‰.2 s (bestArm Î½) â‰¤ âˆ‘ s âˆˆ range m, Ï‰.2 s a} := by
    simp_rw [measureReal_def]
    congr 1
    have : (ğ”“).map (fun Ï‰ â†¦ (âˆ‘ s âˆˆ Icc 1 m, rewardByCount (bestArm Î½) s Ï‰.1 Ï‰.2,
          âˆ‘ s âˆˆ Icc 1 m, rewardByCount a s Ï‰.1 Ï‰.2))
        = (ğ”“).map (fun Ï‰ â†¦ (âˆ‘ s âˆˆ range m, Ï‰.2 s (bestArm Î½), âˆ‘ s âˆˆ range m, Ï‰.2 s a)) :=
      (identDistrib_aux m (bestArm Î½) a).map_eq
    rw [Measure.ext_iff] at this
    have h_meas : MeasurableSet {x : â„ Ã— â„ | x.1 â‰¤ x.2} :=
      measurableSet_le (by fun_prop) (by fun_prop)
    specialize this {x | x.1 â‰¤ x.2} h_meas
    rwa [Measure.map_apply (by fun_prop) h_meas, Measure.map_apply (by fun_prop) h_meas] at this
  _ = (Bandit.streamMeasure Î½).real
      {Ï‰ | âˆ‘ s âˆˆ range m, Ï‰ s (bestArm Î½) â‰¤ âˆ‘ s âˆˆ range m, Ï‰ s a} := by
    simp_rw [measureReal_def]
    congr 1
    rw [â† Bandit.snd_measure (etcAlgorithm hK m), Measure.snd_apply]
    Â· rfl
    Â· exact measurableSet_le (by fun_prop) (by fun_prop)
  _ â‰¤ Real.exp (-â†‘m * gap Î½ a ^ 2 / 4) := by
    by_cases ha : a = bestArm Î½
    Â· simp [ha]
    refine (HasSubgaussianMGF.measure_sum_le_sum_le' (cX := fun _ â†¦ 1) (cY := fun _ â†¦ 1)
      ?_ ?_ ?_ ?_ ?_ ?_).trans_eq ?_
    Â· exact iIndepFun_eval_streamMeasure'' Î½ (bestArm Î½)
    Â· exact iIndepFun_eval_streamMeasure'' Î½ a
    Â· intro i him
      simp_rw [integral_eval_streamMeasure]
      refine (hÎ½ (bestArm Î½)).congr_identDistrib ?_
      exact (identDistrib_eval_eval_id_streamMeasure _ _ _).symm.sub_const _
    Â· intro i him
      simp_rw [integral_eval_streamMeasure]
      refine (hÎ½ a).congr_identDistrib ?_
      exact (identDistrib_eval_eval_id_streamMeasure _ _ _).symm.sub_const _
    Â· exact indepFun_eval_streamMeasure' Î½ (Ne.symm ha)
    Â· gcongr 1 with i him
      simp_rw [integral_eval_streamMeasure]
      exact le_bestArm a
    Â· congr 1
      simp_rw [integral_eval_streamMeasure]
      simp only [id_eq, sum_const, card_range, nsmul_eq_mul, mul_one, NNReal.coe_natCast,
        gap_eq_bestArm_sub, neg_mul]
      field_simp
      ring

lemma expectation_pullCount_le (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) 1 (Î½ a))
    (a : Fin K) (hm : m â‰  0) {n : â„•} (hn : K * m â‰¤ n) :
    ğ”“t[fun Ï‰ â†¦ (pullCount a n Ï‰ : â„)]
      â‰¤ m + (n - K * m) * Real.exp (- (m : â„) * gap Î½ a ^ 2 / 4) := by
  have : (fun Ï‰ â†¦ (pullCount a n Ï‰ : â„))
      =áµ[ğ”“t] fun Ï‰ â†¦ m + (n - K * m) * {Ï‰' | arm (K * m) Ï‰' = a}.indicator (fun _ â†¦ 1) Ï‰ := by
    filter_upwards [pullCount_of_ge a hm hn] with Ï‰ h
    simp only [h, Set.indicator_apply, Set.mem_setOf_eq, mul_ite, mul_one, mul_zero, Nat.cast_add,
      Nat.cast_ite, CharP.cast_eq_zero, add_right_inj]
    norm_cast
  rw [integral_congr_ae this, integral_add (integrable_const _), integral_const_mul]
  swap
  Â· refine Integrable.const_mul ?_ _
    rw [integrable_indicator_iff]
    Â· exact integrableOn_const
    Â· exact (measurableSet_singleton _).preimage (by fun_prop)
  simp only [integral_const, probReal_univ, smul_eq_mul, one_mul, neg_mul,
    add_le_add_iff_left, ge_iff_le]
  gcongr
  Â· norm_cast
    simp
  rw [integral_indicator_const, smul_eq_mul, mul_one]
  Â· rw [â† neg_mul]
    exact prob_arm_mul_eq_le hÎ½ a hm
  Â· exact (measurableSet_singleton _).preimage (by fun_prop)

lemma integrable_pullCount (a : Fin K) (n : â„•) : Integrable (fun Ï‰ â†¦ (pullCount a n Ï‰ : â„)) ğ”“t := by
  refine integrable_of_le_of_le (gâ‚ := 0) (gâ‚‚ := fun _ â†¦ n) (by fun_prop)
    (ae_of_all _ fun Ï‰ â†¦ by simp) (ae_of_all _ fun Ï‰ â†¦ ?_) (integrable_const _) (integrable_const _)
  simp only [Nat.cast_le]
  exact pullCount_le a n Ï‰

lemma regret_le (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) 1 (Î½ a)) (hm : m â‰  0)
    (n : â„•) (hn : K * m â‰¤ n) :
    ğ”“t[regret Î½ n] â‰¤ âˆ‘ a, gap Î½ a * (m + (n - K * m) * Real.exp (- (m : â„) * gap Î½ a ^ 2 / 4)) := by
  simp_rw [regret_eq_sum_pullCount_mul_gap]
  rw [integral_finset_sum]
  swap; Â· exact fun i _ â†¦ (integrable_pullCount i n).mul_const _
  gcongr with a
  rw [mul_comm (gap _ _), integral_mul_const]
  gcongr
  Â· exact gap_nonneg
  Â· exact expectation_pullCount_le hÎ½ a hm hn

end ETC

end Bandits
