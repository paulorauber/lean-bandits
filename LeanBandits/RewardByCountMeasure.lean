/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import LeanBandits.Bandit.Bandit
import LeanBandits.Bandit.Regret
import LeanBandits.ForMathlib.IdentDistrib
import LeanBandits.ForMathlib.IndepFun

/-! # Laws of `stepsUntil` and `rewardByCount`
-/

open MeasureTheory ProbabilityTheory Finset Learning
open scoped ENNReal NNReal

namespace Bandits

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} [DecidableEq Î±] [MeasurableSingletonClass Î±]

@[fun_prop]
lemma measurable_pullCount (a : Î±) (t : â„•) : Measurable (fun h â†¦ pullCount a t h) := by
  simp_rw [pullCount_eq_sum]
  have h_meas s : Measurable (fun h : â„• â†’ Î± Ã— â„ â†¦ if arm s h = a then 1 else 0) := by
    refine Measurable.ite ?_ (by fun_prop) (by fun_prop)
    exact (measurableSet_singleton _).preimage (by fun_prop)
  fun_prop

@[fun_prop]
lemma measurable_sumRewards (a : Î±) (t : â„•) : Measurable (sumRewards a t) := by
  unfold sumRewards
  have h_meas s : Measurable (fun h : â„• â†’ Î± Ã— â„ â†¦ if arm s h = a then reward s h else 0) := by
    refine Measurable.ite ?_ (by fun_prop) (by fun_prop)
    exact (measurableSet_singleton _).preimage (by fun_prop)
  fun_prop

@[fun_prop]
lemma measurable_stepsUntil (a : Î±) (m : â„•) : Measurable (fun h â†¦ stepsUntil a m h) := by
  classical
  have h_union : {h' | âˆƒ s, pullCount a (s + 1) h' = m}
      = â‹ƒ s : â„•, {h' | pullCount a (s + 1) h' = m} := by ext; simp
  have h_meas_set : MeasurableSet {h' | âˆƒ s, pullCount a (s + 1) h' = m} := by
    rw [h_union]
    exact MeasurableSet.iUnion fun s â†¦ (measurableSet_singleton _).preimage (by fun_prop)
  simp_rw [stepsUntil_eq_dite]
  suffices Measurable fun k â†¦ if h : k âˆˆ {k' | âˆƒ s, pullCount a (s + 1) k' = m}
      then (Nat.find h : â„•âˆ) else âŠ¤ by convert this
  refine Measurable.dite (s := {k' : â„• â†’ Î± Ã— â„ | âˆƒ s, pullCount a (s + 1) k' = m})
    (f := fun x â†¦ (Nat.find x.2 : â„•âˆ)) (g := fun _ â†¦ âŠ¤) ?_ (by fun_prop) h_meas_set
  refine Measurable.coe_nat_enat ?_
  refine measurable_find _ fun k â†¦ ?_
  suffices MeasurableSet {x : â„• â†’ Î± Ã— â„ | pullCount a (k + 1) x = m} by
    have : Subtype.val ''
          {x : {k' : â„• â†’ Î± Ã— â„ | âˆƒ s, pullCount a (s + 1) k' = m} | pullCount a (k + 1) x = m}
        = {x : â„• â†’ Î± Ã— â„ | pullCount a (k + 1) x = m} := by
      ext x
      simp only [Set.mem_setOf_eq, Set.coe_setOf, Set.mem_image, Subtype.exists, exists_and_left,
        exists_prop, exists_eq_right_right, and_iff_left_iff_imp]
      exact fun h â†¦ âŸ¨_, hâŸ©
    refine (MeasurableEmbedding.subtype_coe h_meas_set).measurableSet_image.mp ?_
    rw [this]
    exact (measurableSet_singleton _).preimage (by fun_prop)
  exact (measurableSet_singleton _).preimage (by fun_prop)

lemma measurable_stepsUntil' (a : Î±) (m : â„•) :
    Measurable (fun Ï‰ : (â„• â†’ Î± Ã— â„) Ã— (â„• â†’ Î± â†’ â„) â†¦ stepsUntil a m Ï‰.1) :=
  (measurable_stepsUntil a m).comp measurable_fst

@[fun_prop]
lemma measurable_rewardByCount (a : Î±) (m : â„•) :
    Measurable (fun Ï‰ : (â„• â†’ Î± Ã— â„) Ã— (â„• â†’ Î± â†’ â„) â†¦ rewardByCount a m Ï‰.1 Ï‰.2) := by
  simp_rw [rewardByCount_eq_ite]
  refine Measurable.ite ?_ ?_ ?_
  Â· exact (measurableSet_singleton _).preimage <| measurable_stepsUntil' a m
  Â· fun_prop
  Â· change Measurable ((fun p : â„• Ã— (â„• â†’ Î± Ã— â„) â†¦ reward p.1 p.2)
      âˆ˜ (fun Ï‰ : (â„• â†’ Î± Ã— â„) Ã— (â„• â†’ Î± â†’ â„) â†¦ ((stepsUntil a m Ï‰.1).toNat, Ï‰.1)))
    have : Measurable fun Ï‰ : (â„• â†’ Î± Ã— â„) Ã— (â„• â†’ Î± â†’ â„) â†¦ ((stepsUntil a m Ï‰.1).toNat, Ï‰.1) :=
      (measurable_stepsUntil' a m).toNat.prodMk (by fun_prop)
    exact Measurable.comp (by fun_prop) this

variable {alg : Algorithm Î± â„} {Î½ : Kernel Î± â„} [IsMarkovKernel Î½]

omit [DecidableEq Î±] [MeasurableSingletonClass Î±] in
lemma hasLaw_Z (a : Î±) (m : â„•) :
  HasLaw (fun Ï‰ â†¦ Ï‰.2 m a) (Î½ a) (Bandit.measure alg Î½) where
  map_eq := by
    calc ((Bandit.trajMeasure alg Î½).prod (Bandit.streamMeasure Î½)).map (fun Ï‰ â†¦ Ï‰.2 m a)
    _ = (((Bandit.trajMeasure alg Î½).prod (Bandit.streamMeasure Î½)).map (fun Ï‰ â†¦ Ï‰.2)).map
        (fun Ï‰ â†¦ Ï‰ m a) := by
      rw [Measure.map_map (by fun_prop) (by fun_prop)]
      rfl
    _ = (Bandit.streamMeasure Î½).map (fun Ï‰ â†¦ Ï‰ m a) := by simp [Measure.map_snd_prod]
    _ = ((Measure.infinitePi fun _ â†¦ Measure.infinitePi Î½).map (fun Ï‰ â†¦ Ï‰ m)).map
        (fun Ï‰ â†¦ Ï‰ a) := by
      rw [Bandit.streamMeasure, Measure.map_map (by fun_prop) (by fun_prop)]
      rfl
    _ = Î½ a := by simp_rw [(measurePreserving_eval_infinitePi _ _).map_eq]

/-- Law of `Y` conditioned on the event `s`.-/
notation "ğ“›[" Y " | " s "; " Î¼ "]" => Measure.map Y (Î¼[|s])
/-- Law of `Y` conditioned on the event that `X` is in `s`. -/
notation "ğ“›[" Y " | " X " in " s "; " Î¼ "]" => Measure.map Y (Î¼[|X â»Â¹' s])
/-- Law of `Y` conditioned on the event that `X` equals `x`. -/
notation "ğ“›[" Y " | " X " â† " x "; " Î¼ "]" => Measure.map Y (Î¼[|X â»Â¹' {x}])

omit [DecidableEq Î±] [MeasurableSingletonClass Î±] in
lemma condDistrib_reward'' [StandardBorelSpace Î±] [Nonempty Î±] (n : â„•) :
    ğ“›[fun Ï‰ â†¦ reward n Ï‰.1 | fun Ï‰ â†¦ arm n Ï‰.1; Bandit.measure alg Î½]
      =áµ[(Bandit.measure alg Î½).map (fun Ï‰ â†¦ arm n Ï‰.1)] Î½ := by
  let Î¼ := Bandit.measure alg Î½
  have h_ra' : ğ“›[reward n | arm n; Bandit.trajMeasure alg Î½]
      =áµ[(Bandit.trajMeasure alg Î½).map (arm n)] Î½ := condDistrib_reward alg Î½ n
  have h_law : Î¼.map (fun Ï‰ â†¦ arm n Ï‰.1) = (Bandit.trajMeasure alg Î½).map (arm n) := by
    calc Î¼.map (fun Ï‰ â†¦ arm n Ï‰.1)
    _ = (Î¼.map (fun Ï‰ â†¦ Ï‰.1)).map (fun Ï‰ â†¦ arm n Ï‰) := by
      rw [Measure.map_map (by fun_prop) (by fun_prop)]
      rfl
    _ = _ := by unfold Î¼ Bandit.measure; simp [Measure.map_fst_prod]
  rw [h_law]
  have h_prod : ğ“›[fun Ï‰ â†¦ reward n Ï‰.1 | fun Ï‰ â†¦ arm n Ï‰.1; Î¼]
      =áµ[(Bandit.trajMeasure alg Î½).map (arm n)] ğ“›[reward n | arm n; Bandit.trajMeasure alg Î½] :=
    condDistrib_fst_prod _ (by fun_prop) _
  filter_upwards [h_ra', h_prod] with Ï‰ h_eq h_prod
  rw [h_prod, h_eq]

omit [DecidableEq Î±] in
lemma reward_cond_arm [StandardBorelSpace Î±] [Nonempty Î±] [Countable Î±] (a : Î±) (n : â„•)
    (hÎ¼a : (Bandit.measure alg Î½).map (fun Ï‰ â†¦ arm n Ï‰.1) {a} â‰  0) :
    ğ“›[fun Ï‰ â†¦ reward n Ï‰.1 | fun Ï‰ â†¦ arm n Ï‰.1 â† a; Bandit.measure alg Î½] = Î½ a := by
  let Î¼ := Bandit.measure alg Î½
  have h_ra : ğ“›[fun Ï‰ â†¦ reward n Ï‰.1 | fun Ï‰ â†¦ arm n Ï‰.1; Î¼] =áµ[Î¼.map (fun Ï‰ â†¦ arm n Ï‰.1)] Î½ :=
    condDistrib_reward'' n
  have h_eq := condDistrib_ae_eq_cond (Î¼ := Î¼)
    (X := fun Ï‰ â†¦ arm n Ï‰.1) (Y := fun Ï‰ â†¦ reward n Ï‰.1) (by fun_prop) (by fun_prop)
  rw [Filter.EventuallyEq, ae_iff_of_countable] at h_ra h_eq
  specialize h_ra a hÎ¼a
  specialize h_eq a hÎ¼a
  rw [h_ra] at h_eq
  exact h_eq.symm

-- after the Mathlib stopping time refactor, we will be able to prove that stepsUntil is a
-- stopping time
lemma measurable_comap_indicator_stepsUntil_eq (a : Î±) (m n : â„•) :
    Measurable[MeasurableSpace.comap (fun Ï‰ : â„• â†’ Î± Ã— â„ â†¦ (hist (n-1) Ï‰, arm n Ï‰)) inferInstance]
      ({Ï‰ | stepsUntil a m Ï‰ = â†‘n}.indicator fun _ â†¦ 1) := by
  let k : ((Iic (n - 1) â†’ Î± Ã— â„) Ã— Î±) â†’ (â„• â†’ Î± Ã— â„) := fun x i â†¦
    if hi : i âˆˆ Iic (n - 1) then (x.1 âŸ¨i, hiâŸ©) else if i = n then (x.2, 0) else (a, 0)
  have hk : Measurable k := by
    unfold k
    rw [measurable_pi_iff]
    intro i
    split_ifs <;> fun_prop
  let Ï† : ((Iic (n - 1) â†’ Î± Ã— â„) Ã— Î±) â†’ â„• := fun x â†¦ if stepsUntil a m (k x) = â†‘n then 1 else 0
  have hÏ† : Measurable Ï† :=
    Measurable.ite ((measurableSet_singleton _).preimage (by fun_prop)) (by fun_prop) (by fun_prop)
  suffices {Ï‰ | stepsUntil a m Ï‰ = â†‘n}.indicator (fun x â†¦ 1)
      = Ï† âˆ˜ fun Ï‰ â†¦ (hist (n - 1) Ï‰, arm n Ï‰) from this â–¸ measurable_comp_comap _ hÏ†
  ext Ï‰
  classical
  simp only [Set.indicator_apply, Set.mem_setOf_eq, Function.comp_apply, Ï†]
  congr 1
  rw [stepsUntil_eq_congr]
  intro i hin
  simp only [arm, mem_Iic, hist, dite_eq_ite, k]
  grind

lemma measurable_indicator_stepsUntil_eq (a : Î±) (m n : â„•) :
    Measurable ({Ï‰ | stepsUntil a m Ï‰ = â†‘n}.indicator fun _ â†¦ 1) := by
  refine (measurable_comap_indicator_stepsUntil_eq a m n).mono ?_ le_rfl
  refine Measurable.comap_le ?_
  fun_prop

lemma measurableSet_stepsUntil_eq (a : Î±) (m n : â„•) :
    MeasurableSet[MeasurableSpace.comap (fun Ï‰ : â„• â†’ Î± Ã— â„ â†¦ (hist (n-1) Ï‰, arm n Ï‰)) inferInstance]
      {Ï‰ : â„• â†’ Î± Ã— â„ | stepsUntil a m Ï‰ = â†‘n} := by
  let mProd := MeasurableSpace.comap (fun Ï‰ : â„• â†’ Î± Ã— â„ â†¦ (hist (n-1) Ï‰, arm n Ï‰)) inferInstance
  suffices Measurable[mProd] ({Ï‰ | stepsUntil a m Ï‰ = â†‘n}.indicator fun x â†¦ 1) by
    rwa [measurable_indicator_const_iff] at this
  exact measurable_comap_indicator_stepsUntil_eq a m n

lemma condIndepFun_reward_stepsUntil_arm' [StandardBorelSpace Î±] [Countable Î±] [Nonempty Î±]
    (a : Î±) (m n : â„•) (hm : m â‰  0) :
    CondIndepFun (mÎ±.comap (arm n)) (measurable_arm n).comap_le
      (reward n) ({Ï‰ | stepsUntil a m Ï‰ = â†‘n}.indicator (fun _ â†¦ 1))
      (Bandit.trajMeasure alg Î½) := by
  -- the indicator of `stepsUntil ... = n` is a function of
  -- `hist (n-1)` and `arm n`.
  -- It thus suffices to prove the independence of `reward n` and `hist (n-1)` conditionally
  -- on `arm n`.
  by_cases hn : n = 0
  Â· simp only [hn, CharP.cast_eq_zero]
    simp only [stepsUntil_eq_zero_iff, hm, ne_eq, false_and, false_or]
    by_cases hm1 : m = 1
    Â· simp only [hm1, true_and]
      have h_indep := condIndepFun_self_right (X := reward 0) (Z := arm 0)
        (mÎ² := inferInstance) (mÎ²' := inferInstance) (Î¼ := Bandit.trajMeasure alg Î½)
        (by fun_prop) (by fun_prop)
      have : {Ï‰ : â„• â†’ Î± Ã— â„ | arm 0 Ï‰ = a}.indicator (fun x â†¦ 1)
          = {b | b = a}.indicator (fun _ â†¦ 1) âˆ˜ arm 0 := by ext; simp [Set.indicator]
      rw [this]
      exact h_indep.comp measurable_id (by fun_prop)
    Â· simp only [hm1, false_and, Set.setOf_false, Set.indicator_empty]
      exact condIndepFun_const_right (reward 0) 0
  have h_indep : CondIndepFun (mÎ±.comap (arm n)) (measurable_arm n).comap_le (reward n)
      (hist (n - 1)) (Bandit.trajMeasure alg Î½) := by
    convert condIndepFun_reward_hist_arm (alg := alg) (Î½ := Î½) (n - 1)
      <;> rw [Nat.sub_add_cancel (by grind)]
  have h_indep' : CondIndepFun (mÎ±.comap (arm n)) (measurable_arm n).comap_le (reward n)
      (fun Ï‰ â†¦ (hist (n - 1) Ï‰, arm n Ï‰)) (Bandit.trajMeasure alg Î½) :=
    h_indep.prod_right (by fun_prop) (by fun_prop) (by fun_prop)
  obtain âŸ¨Ï†, hÏ†_meas, h_eqâŸ© : âˆƒ Ï† : ((Iic (n - 1) â†’ Î± Ã— â„) Ã— Î±) â†’ â„•, Measurable Ï† âˆ§
      {Ï‰ | stepsUntil a m Ï‰ = â†‘n}.indicator (fun _ â†¦ 1) = Ï† âˆ˜ (fun Ï‰ â†¦ (hist (n - 1) Ï‰, arm n Ï‰)) :=
    (measurable_comap_indicator_stepsUntil_eq a m n).exists_eq_measurable_comp
  rw [h_eq]
  exact h_indep'.comp measurable_id hÏ†_meas

lemma condIndepFun_reward_stepsUntil_arm [StandardBorelSpace Î±] [Countable Î±] [Nonempty Î±]
    (a : Î±) (m n : â„•) (hm : m â‰  0) :
    CondIndepFun (mÎ±.comap (fun Ï‰ â†¦ arm n Ï‰.1)) ((measurable_arm n).comp measurable_fst).comap_le
      (fun Ï‰ â†¦ reward n Ï‰.1) ({Ï‰ | stepsUntil a m Ï‰.1 = â†‘n}.indicator (fun _ â†¦ 1))
      (Bandit.measure alg Î½) :=
  condIndepFun_fst_prod (Î½ := Bandit.streamMeasure Î½)
    (measurable_indicator_stepsUntil_eq a m n) (by fun_prop) (by fun_prop)
    (condIndepFun_reward_stepsUntil_arm' a m n hm)

lemma reward_cond_stepsUntil [StandardBorelSpace Î±] [Countable Î±] [Nonempty Î±] (a : Î±) (m n : â„•)
    (hm : m â‰  0)
    (hÎ¼n : (Bandit.measure alg Î½) ((fun Ï‰ â†¦ stepsUntil a m Ï‰.1) â»Â¹' {â†‘n}) â‰  0) :
    ğ“›[fun Ï‰ â†¦ reward n Ï‰.1 | fun Ï‰ â†¦ stepsUntil a m Ï‰.1 â† â†‘n; Bandit.measure alg Î½] = Î½ a := by
  let Î¼ := Bandit.measure alg Î½
  have hÎ¼na :
      Î¼ ((fun Ï‰ â†¦ stepsUntil a m Ï‰.1) â»Â¹' {â†‘n} âˆ© (fun Ï‰ â†¦ arm n Ï‰.1) â»Â¹' {a}) â‰  0 := by
    suffices ((fun Ï‰ : (â„• â†’ Î± Ã— â„) Ã— (â„• â†’ Î± â†’ â„) â†¦
          stepsUntil a m Ï‰.1) â»Â¹' {â†‘n} âˆ© (fun Ï‰ â†¦ arm n Ï‰.1) â»Â¹' {a})
        = (fun Ï‰ â†¦ stepsUntil a m Ï‰.1) â»Â¹' {â†‘n} by simpa [this] using hÎ¼n
    ext Ï‰
    simp only [Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff, and_iff_left_iff_imp]
    exact arm_eq_of_stepsUntil_eq_coe hm
  have hÎ¼a : Î¼.map (fun Ï‰ â†¦ arm n Ï‰.1) {a} â‰  0 := by
    rw [Measure.map_apply (by fun_prop) (measurableSet_singleton _)]
    refine fun h_zero â†¦ hÎ¼n (measure_mono_null (fun Ï‰ â†¦ ?_) h_zero)
    simp only [Set.mem_preimage, Set.mem_singleton_iff]
    exact arm_eq_of_stepsUntil_eq_coe hm
  calc ğ“›[fun Ï‰ â†¦ reward n Ï‰.1 | fun Ï‰ â†¦ stepsUntil a m Ï‰.1 â† (n : â„•âˆ); Î¼]
  _ = (Î¼[|(fun Ï‰ â†¦ stepsUntil a m Ï‰.1) â»Â¹' {â†‘n} âˆ© (fun Ï‰ â†¦ arm n Ï‰.1) â»Â¹' {a}]).map
      (fun Ï‰ â†¦ reward n Ï‰.1) := by
    congr with Ï‰
    simp only [Set.mem_preimage, Set.mem_singleton_iff, Set.mem_inter_iff, iff_self_and]
    exact arm_eq_of_stepsUntil_eq_coe hm
  _ = (Î¼[|(fun Ï‰ â†¦ arm n Ï‰.1) â»Â¹' {a}
      âˆ© {Ï‰ : (â„• â†’ Î± Ã— â„) Ã— (â„• â†’ Î± â†’ â„) | stepsUntil a m Ï‰.1 = â†‘n}.indicator 1 â»Â¹' {1} ]).map
      (fun Ï‰ â†¦ reward n Ï‰.1) := by
    congr 2 with Ï‰
    simp only [Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff, Set.indicator_apply,
      Set.mem_setOf_eq, Pi.one_apply, ite_eq_left_iff, zero_ne_one, imp_false, Decidable.not_not]
    rw [and_comm]
  _ = ğ“›[fun Ï‰ â†¦ reward n Ï‰.1 | fun Ï‰ â†¦ arm n Ï‰.1 â† a; Î¼] := by
    rw [cond_of_condIndepFun (by fun_prop)]
    Â· exact condIndepFun_reward_stepsUntil_arm a m n hm
    Â· refine measurable_one.indicator ?_
      exact measurableSet_eq_fun (by fun_prop) (by fun_prop)
    Â· fun_prop
    Â· convert hÎ¼na using 2
      rw [Set.inter_comm]
      congr 1 with Ï‰
      simp [Set.indicator_apply]
  _ = Î½ a := reward_cond_arm a n hÎ¼a

lemma condDistrib_rewardByCount_stepsUntil [Countable Î±] [StandardBorelSpace Î±] [Nonempty Î±]
    (a : Î±) (m : â„•) (hm : m â‰  0) :
    condDistrib (fun Ï‰ â†¦ rewardByCount a m Ï‰.1 Ï‰.2) (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)
        (Bandit.measure alg Î½)
      =áµ[(Bandit.measure alg Î½).map (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)] Kernel.const _ (Î½ a) := by
  let Î¼ := Bandit.measure alg Î½
  refine (condDistrib_ae_eq_cond (Î¼ := Î¼)
    (X := fun Ï‰ â†¦ stepsUntil a m Ï‰.1) (by fun_prop) (by fun_prop)).trans ?_
  rw [Filter.EventuallyEq, ae_iff_of_countable]
  intro n hn
  simp only [Kernel.const_apply]
  cases n with
  | top =>
    rw [Measure.map_congr (g := fun Ï‰ â†¦ Ï‰.2 m a)]
    swap
    Â· refine ae_cond_of_forall_mem ((measurableSet_singleton _).preimage (by fun_prop)) ?_
      simp only [Set.mem_preimage, Set.mem_singleton_iff]
      exact fun Ï‰ â†¦ rewardByCount_of_stepsUntil_eq_top
    rw [cond_of_indepFun _ (by fun_prop) (by fun_prop) (measurableSet_singleton _)]
    Â· exact (hasLaw_Z a m).map_eq
    Â· rwa [Measure.map_apply (by fun_prop) (measurableSet_singleton _)] at hn
    Â· exact indepFun_prod (X := fun Ï‰ : â„• â†’ Î± Ã— â„ â†¦ stepsUntil a m Ï‰)
        (Y := fun Ï‰ : â„• â†’ Î± â†’ â„ â†¦ Ï‰ m a) (by fun_prop) (by fun_prop)
  | coe n =>
    rw [Measure.map_congr (g := fun Ï‰ â†¦ reward n Ï‰.1)]
    swap
    Â· refine ae_cond_of_forall_mem ((measurableSet_singleton _).preimage (by fun_prop)) ?_
      simp only [Set.mem_preimage, Set.mem_singleton_iff]
      exact fun Ï‰ â†¦ rewardByCount_of_stepsUntil_eq_coe
    refine reward_cond_stepsUntil a m n hm ?_
    rwa [Measure.map_apply (by fun_prop) (measurableSet_singleton _)] at hn

/-- The reward received at the `m`-th pull of arm `a` has law `Î½ a`. -/
lemma hasLaw_rewardByCount [Countable Î±] [StandardBorelSpace Î±] [Nonempty Î±]
    (a : Î±) (m : â„•) (hm : m â‰  0) :
    HasLaw (fun Ï‰ â†¦ rewardByCount a m Ï‰.1 Ï‰.2) (Î½ a) (Bandit.measure alg Î½) where
  map_eq := by
    have h_condDistrib :
        condDistrib (fun Ï‰ â†¦ rewardByCount a m Ï‰.1 Ï‰.2) (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)
          (Bandit.measure alg Î½)
        =áµ[(Bandit.measure alg Î½).map (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)]
          Kernel.const _ (Î½ a) := condDistrib_rewardByCount_stepsUntil a m hm
    calc (Bandit.measure alg Î½).map (fun Ï‰ â†¦ rewardByCount a m Ï‰.1 Ï‰.2)
    _ = (condDistrib (fun Ï‰ â†¦ rewardByCount a m Ï‰.1 Ï‰.2) (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)
          (Bandit.measure alg Î½))
        âˆ˜â‚˜ ((Bandit.measure alg Î½).map (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)) := by
      rw [condDistrib_comp_map (by fun_prop) (by fun_prop)]
    _ = (Kernel.const _ (Î½ a))
        âˆ˜â‚˜ ((Bandit.measure alg Î½).map (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)) :=
      Measure.comp_congr h_condDistrib
    _ = Î½ a := by
      have : IsProbabilityMeasure
          ((Bandit.measure alg Î½).map (fun Ï‰ â†¦ stepsUntil a m Ï‰.1)) :=
        Measure.isProbabilityMeasure_map (by fun_prop)
      simp

lemma identDistrib_rewardByCount [Countable Î±] [StandardBorelSpace Î±] [Nonempty Î±] (a : Î±) (n m : â„•)
    (hn : n â‰  0) (hm : m â‰  0) :
    IdentDistrib (fun Ï‰ â†¦ rewardByCount a n Ï‰.1 Ï‰.2) (fun Ï‰ â†¦ rewardByCount a m Ï‰.1 Ï‰.2)
      (Bandit.measure alg Î½) (Bandit.measure alg Î½) where
  aemeasurable_fst := by fun_prop
  aemeasurable_snd := by fun_prop
  map_eq := by rw [(hasLaw_rewardByCount a n hn).map_eq, (hasLaw_rewardByCount a m hm).map_eq]

lemma identDistrib_rewardByCount_id [Countable Î±] [StandardBorelSpace Î±] [Nonempty Î±]
    (a : Î±) (n : â„•) (hn : n â‰  0) :
    IdentDistrib (fun Ï‰ â†¦ rewardByCount a n Ï‰.1 Ï‰.2) id (Bandit.measure alg Î½) (Î½ a) where
  aemeasurable_fst := by fun_prop
  aemeasurable_snd := Measurable.aemeasurable <| by fun_prop
  map_eq := by rw [(hasLaw_rewardByCount a n hn).map_eq, Measure.map_id]

lemma identDistrib_rewardByCount_eval [Countable Î±] [StandardBorelSpace Î±] [Nonempty Î±]
    (a : Î±) (n m : â„•) (hn : n â‰  0) :
    IdentDistrib (fun Ï‰ â†¦ rewardByCount a n Ï‰.1 Ï‰.2) (fun Ï‰ â†¦ Ï‰ m a)
      (Bandit.measure alg Î½) (Bandit.streamMeasure Î½) :=
  (identDistrib_rewardByCount_id a n hn).trans (identDistrib_eval_eval_id_streamMeasure Î½ m a).symm

lemma indepFun_rewardByCount_Iic (alg : Algorithm Î± â„) (Î½ : Kernel Î± â„) [IsMarkovKernel Î½] (a : Î±)
    (n : â„•) :
    (fun Ï‰ â†¦ rewardByCount a (n + 1) Ï‰.1 Ï‰.2) âŸ‚áµ¢[Bandit.measure alg Î½]
      fun Ï‰ (i : Iic n) â†¦ rewardByCount a i Ï‰.1 Ï‰.2 := by
  sorry

lemma iIndepFun_rewardByCount' (alg : Algorithm Î± â„) (Î½ : Kernel Î± â„) [IsMarkovKernel Î½] (a : Î±) :
    iIndepFun (fun n Ï‰ â†¦ rewardByCount a n Ï‰.1 Ï‰.2) (Bandit.measure alg Î½) := by
  rw [iIndepFun_nat_iff_forall_indepFun (by fun_prop)]
  exact indepFun_rewardByCount_Iic alg Î½ a

lemma iIndepFun_rewardByCount (alg : Algorithm Î± â„) (Î½ : Kernel Î± â„) [IsMarkovKernel Î½] :
    iIndepFun (fun (p : Î± Ã— â„•) Ï‰ â†¦ rewardByCount p.1 p.2 Ï‰.1 Ï‰.2) (Bandit.measure alg Î½) := by
  sorry

lemma identDistrib_rewardByCount_stream' [Countable Î±] [StandardBorelSpace Î±] [Nonempty Î±]
    (a : Î±) :
    IdentDistrib (fun Ï‰ n â†¦ rewardByCount a (n + 1) Ï‰.1 Ï‰.2) (fun Ï‰ n â†¦ Ï‰ n a)
      (Bandit.measure alg Î½) (Bandit.streamMeasure Î½) := by
  refine IdentDistrib.pi (fun n â†¦ ?_) ?_ ?_
  Â· refine identDistrib_rewardByCount_eval a (n + 1) n (by simp) (Î½ := Î½)
  Â· have h_indep := iIndepFun_rewardByCount' alg Î½ a
    exact iIndepFun.precomp (g := fun n â†¦ n + 1) (fun i j hij â†¦ by grind) h_indep
  Â· exact iIndepFun_eval_streamMeasure'' Î½ a

lemma identDistrib_rewardByCount_stream [Countable Î±] [StandardBorelSpace Î±] [Nonempty Î±]
    (a : Î±) :
    IdentDistrib (fun Ï‰ n â†¦ rewardByCount a (n + 1) Ï‰.1 Ï‰.2) (fun Ï‰ n â†¦ Ï‰.2 n a)
      (Bandit.measure alg Î½) (Bandit.measure alg Î½) := by
  refine (identDistrib_rewardByCount_stream' a).trans ?_
  refine IdentDistrib.pi (fun n â†¦ ?_) ?_ ?_
  Â· rw [â† Bandit.snd_measure alg Î½, Measure.snd,
      identDistrib_map_left_iff (by fun_prop) (by fun_prop)
        (Measurable.aemeasurable <| by fun_prop)]
    exact IdentDistrib.refl (by fun_prop)
  Â· exact iIndepFun_eval_streamMeasure'' Î½ a
  Â· change iIndepFun (fun n â†¦ ((fun Ï‰ â†¦ Ï‰ n a) âˆ˜ Prod.snd)) (Bandit.measure alg Î½)
    rw [â† iIndepFun_map_iff (by fun_prop) (fun _ â†¦ Measurable.aemeasurable (by fun_prop))]
    rw [â† Measure.snd, Bandit.snd_measure]
    exact iIndepFun_eval_streamMeasure'' Î½ a

lemma indepFun_rewardByCount_of_ne {a b : Î±} (hab : a â‰  b) :
    IndepFun (fun Ï‰ s â†¦ rewardByCount a s Ï‰.1 Ï‰.2) (fun Ï‰ s â†¦ rewardByCount b s Ï‰.1 Ï‰.2)
      (Bandit.measure alg Î½) := by
  sorry

end Bandits
